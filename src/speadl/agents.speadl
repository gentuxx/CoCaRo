import CoCaRo.CustomColor
import CoCaRo.agents.IRobotCore
import CoCaRo.agents.behaviour.actions.interfaces.IAgentAction
import CoCaRo.agents.behaviour.decision.interfaces.IAgentDecisionCreator
import CoCaRo.agents.behaviour.perception.interfaces.IAgentPerception

namespace speadl.agents {
	
	ecosystem RobotsEcosystem {
		provides decisionCreator : IAgentDecisionCreator = behaviour.decisionCreator
		
		species Robot(identifier : String, color : 	CustomColor) {
			requires coreR : IRobotCore
			
			use aBehaviour : behaviour.AgentBehaviourPDA(identifier,color){
				bind coreB to coreR	
			}
		}
		
		part behaviour : AgentBehaviour {
				
		}
	}
	
	ecosystem AgentBehaviour {
		
		provides decisionCreator : IAgentDecisionCreator = decision.creator
		
		species AgentBehaviourPDA(identifier : String, color : CustomColor) {
			
			requires coreB: IRobotCore
			
			use aPerc : perception.PerceptionCore() {
				bind core to coreB
			}
			use aAction : actions.ActionCore()
			use aDecision : decision.DecisionCore() {
				bind core to coreB
				bind perception to aPerc.perception
				bind actions to aAction.actions
			}
		}
		
		part perception : AgentPerception {
			
		}
		
		part actions : AgentAction {
			
		}
		
		part decision : AgentDecision {
			
		}
	}
	
	ecosystem AgentPerception {
		
		species PerceptionCore() {
			requires core : IRobotCore
			provides perception : IAgentPerception
		}
	}
	
	ecosystem AgentAction {
		
		species ActionCore() {
			provides actions : IAgentAction
		}
	}
	
	ecosystem AgentDecision {
		provides creator : IAgentDecisionCreator
		
		species DecisionCore() {
			requires perception : IAgentPerception
			requires actions : IAgentAction
			requires core : IRobotCore
		}
	}
}