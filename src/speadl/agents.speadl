import java.Color
import java.agents.IAgentAction
import java.agents.IAgentDecisionCreator
import java.agents.IAgentPerception
import java.environment.IEnvironment

namespace speadl.agents {
	
	ecosystem RobotsEcosystem {
		provides decisionCreator : IAgentDecisionCreator = behaviour.decisionCreator
		
		species Robot(identifier : String, color : Color) {
			requires gridR : IEnvironment
			
			use aBehaviour : behaviour.AgentBehaviourPDA(){
				bind gridB to gridR
			}
		}
		
		part behaviour : AgentBehaviour {
				
		}
	}
	
	ecosystem AgentBehaviour {
		
		provides decisionCreator : IAgentDecisionCreator = decision.creator
		
		species AgentBehaviourPDA() {
			
			requires gridB: IEnvironment
			
			use aPerc : perception.PerceptionCore() {
				bind gridP to gridB
			}
			use aAction : actions.ActionCore()
			use aDecision : decision.DecisionCore() {
				bind env to gridB
				bind perception to aPerc.perception
				bind actions to aAction.actions
			}
		}
		
		part perception : AgentPerception {
			
		}
		
		part actions : AgentAction {
			
		}
		
		part decision : AgentDecision {
			
		}
	}
	
	ecosystem AgentPerception {
		
		species PerceptionCore() {
			requires gridP : IEnvironment
			provides perception : IAgentPerception
		}
	}
	
	ecosystem AgentAction {
		
		species ActionCore() {
			provides actions : IAgentAction
		}
	}
	
	ecosystem AgentDecision {
		provides creator : IAgentDecisionCreator
		
		species DecisionCore() {
			requires perception : IAgentPerception
			requires actions : IAgentAction
			requires env : IEnvironment
		}
	}
}